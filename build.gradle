plugins {
	id 'org.quiltmc.loom' version '1.+'
	id 'maven-publish'
//	alias libs.plugins.quilt.loom
}
archivesBaseName = project.archives_base_name
version = "${project.version}+${libs.versions.minecraft.get()}"
group = project.maven_group

loom {
	mods {
		"pvzmod" {
			sourceSet sourceSets.main
		}
	}
}

repositories {
	mavenCentral()
	maven { url "https://maven.shedaniel.me" }
	maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
	maven {
		name 'Quilt'
		url 'https://maven.quiltmc.org/repository/release'
	}
	maven { url 'https://maven.blamejared.com' }
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
	maven { url 'https://maven.wispforest.io' }
	maven {
		// location of the maven that hosts JEI files since January 2023
		name = "Jared's maven"
		url = "https://maven.blamejared.com/"
	}
	maven {
		// location of a maven mirror for JEI files, as a fallback
		name = "ModMaven"
		url = "https://modmaven.dev"

	}
}

project.configurations.configureEach {
	exclude(group: "net.fabricmc", module: "fabric-loader")
	exclude(group: "net.fabricmc.fabric-api")
}

dependencies {
	modApi("me.shedaniel.cloth:cloth-config-fabric:13.0.121") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modApi("dev.architectury:architectury-fabric:11.1.17")
	minecraft libs.minecraft
	modImplementation "org.quiltmc:quilt-loader:${project.loader_version}"
	mappings "org.quiltmc:quilt-mappings:${minecraft_version}+build.${quilt_mappings}:intermediary-v2"
	modImplementation "org.quiltmc:qsl:${qsl_version}+${minecraft_version}"
	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilted_fabric_api_version}-${minecraft_version}"
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:14.1.714"
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:14.1.714"
	modImplementation 'software.bernie.geckolib:geckolib-fabric-1.20.4:4.4.4'
	modImplementation "vazkii.patchouli:Patchouli:1.20.4-85-FABRIC"
	modImplementation "io.wispforest:owo-lib:0.12.3+1.20.3+"
	annotationProcessor modImplementation("io.wispforest:owo-lib:0.12.3+1.20.3+")
}


processResources {
	inputs.property "version", version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}
tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
	}
}
